buildscript {
    dependencies {
        classpath 'com.bmuschko:gradle-tomcat-plugin:2.2.2'
    }

    repositories {
        jcenter()
        mavenCentral()
    }
}
subprojects {
    apply plugin: 'com.bmuschko.tomcat'

    task integrationTomcatStart(type: com.bmuschko.gradle.tomcat.tasks.TomcatRunWar) {
        daemon = true
        stopPort = 9088
        stopKey = 'stopKey'
        outputs.upToDateWhen { false }
        webApp = file("../../web/build/libs/web-${version}.war")
    }

    task integrationTomcatStop(type: com.bmuschko.gradle.tomcat.tasks.TomcatStop) {
        stopPort = 9088
        stopKey = 'stopKey'
        outputs.upToDateWhen { false }
    }

    task integrationTest(type: Test) {
        dependsOn integrationTomcatStart
        finalizedBy integrationTomcatStop
        doLast {
            System.clearProperty('spring.profiles.active')
        }
    }

    dependencies {
        def tomcatVersion = '7.0.59'
        tomcat group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: tomcatVersion
        tomcat group: 'org.apache.tomcat.embed', name: 'tomcat-embed-logging-juli', version: tomcatVersion
        tomcat group: 'org.apache.tomcat.embed', name: 'tomcat-embed-jasper', version: tomcatVersion
    }
}
